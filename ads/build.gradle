plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file


def getVersionName = { ->
    return "2.7.8" // Replace with version Name
}

def getArtifactId = { ->
    return "ads" // Replace with library name ID
}

repositories {
    maven { url 'https://artifact.bytedance.com/repository/pangle' }
}
afterEvaluate {
    publishing {

        publications {
            release(MavenPublication) {
                components.getByName('release')
                groupId 'com.viyatek.libraries' // Replace with group ID
                artifactId getArtifactId()
                version getVersionName()
                artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")

                pom.withXml {
                    final dependenciesNode = asNode().appendNode('dependencies')
                    ext.addDependency = { Dependency dep, String scope ->
                        if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
                            return
                        final dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dep.group)
                        dependencyNode.appendNode('artifactId', dep.name)
                        dependencyNode.appendNode('version', dep.version)
                        dependencyNode.appendNode('scope', scope)
                        if (!dep.transitive) {
                            final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                            exclusionNode.appendNode('groupId', '*')
                            exclusionNode.appendNode('artifactId', '*')
                        } else if (!dep.properties.excludeRules.empty) {
                            final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                            dep.properties.excludeRules.each { ExcludeRule rule ->
                                exclusionNode.appendNode('groupId', rule.group ?: '*')
                                exclusionNode.appendNode('artifactId', rule.module ?: '*')
                            }
                        }
                    }
                    // configurations.compile.getDependencies().each { dep -> addDependency(dep, "implementation") }
                    configurations.api.getDependencies().each { dep -> addDependency(dep, "implementation") }
                    configurations.implementation.getDependencies().each { dep -> addDependency(dep, "runtime") }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Viya-Teknoloji/viyatek")
                credentials {
                    /** Create github.properties in root project folder file with
                     ** gpr.usr=GITHUB_USER_ID & gpr.key=PERSONAL_ACCESS_TOKEN
                     ** Set env variable GPR_USER & GPR_API_KEY if not adding a properties file**/

                    username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                    password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }

        }
    }
}


android {
    compileSdk 34

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
    }
    ndkVersion '22.0.7026061'
    namespace 'com.viyatek.ads'
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }

        singleVariant('debug') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation 'com.facebook.android:audience-network-sdk:6.14.0'
    implementation 'com.mopub.mediation:facebookaudiencenetwork:6.6.0.0'

    implementation 'com.pangle.global:ads-sdk:5.2.0.3'
    implementation 'com.google.android.gms:play-services-ads:22.1.0'

    implementation 'com.google.android.gms:play-services-ads-identifier:18.0.1'
    implementation 'com.google.android.gms:play-services-base:18.2.0'

    implementation 'com.applovin:applovin-sdk:11.5.2'
  }